#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <https://www.gnu.org/licenses/>.
#

%data secondpass_input.cdat : CELL_COLOR_LIMIT, MAX_SPR_PALETTES, OVERLAY_ROW_SIZE_LIMIT, XRANGE set, YRANGE set, COLORS set, layerColors[XRANGE, YRANGE, COLORS], layerColorColumnCount[XRANGE, YRANGE, COLORS]

parameters:
    MAX_COLORS_OVERLAY  := MAX_SPR_PALETTES * CELL_COLOR_LIMIT;

variables:
    # Decision variables represent for each possible color
    # - 0 if this grid cell does not have the color in the layer
    # - 1 if this grid cell has the color in the layer
    colorsOverlay[XRANGE, YRANGE, COLORS] : binary;
    colorsOverlayGrid[XRANGE, YRANGE, COLORS] : binary;
    colorsOverlayFree[XRANGE, YRANGE, COLORS] : binary;
    # Overlay occupancy is 1 if any color in this cell is inside overlay
    occupancy[XRANGE, YRANGE] : binary;
    # overlay total is 1 whenever a particular color is anywhere in the overlay
    colorsOverlayTotal[COLORS] : binary;
    # Represents colors present in each palette
    palettesOverlay[0..MAX_SPR_PALETTES-1, COLORS] : binary;
    # Represents whether colors in a cell are a subset of each palette
    usesPaletteOverlay[XRANGE, YRANGE, 0..MAX_SPR_PALETTES-1] : binary;

objectives:
    # Goal: Minimise free sprites
    sum{ x in XRANGE, y in YRANGE, c in COLORS: layerColorColumnCount[x, y, c] * colorsOverlayFree[x, y, c] } -> min;

constraints:
    # Constraints ensuring that a BG cell can have no more colors than the cell color limit	 
    { x in XRANGE, y in YRANGE:
        cellColorLimit_$x$_$y$:
            sum{ c in COLORS: colorsOverlayGrid[x, y, c] } <= CELL_COLOR_LIMIT;
    }

    # Constraints ensuring that a color must be in one-and-only-one of grid-overlay or free-overlay
    { x in XRANGE, y in YRANGE:
        color_InEitherGridOrFreeOverlay_$x$_$y$_$2$:
            colorsOverlayGrid[x, y, COLORS] + colorsOverlayFree[x, y, COLORS] = colorsOverlay[x, y, COLORS];
        colorsOverlayEqualToLayerColors_$x$_$y$_$2$:
            colorsOverlay[x, y, COLORS] = layerColors[x, y, COLORS];
    }

    # Constraints for occupancy (logical OR between all colors in colorsOverlayGrid, reformulated in LP)
    { x in XRANGE, y in YRANGE:
        occupancy_$x$_$y$:
            occupancy[x, y] >= colorsOverlayGrid[x, y, COLORS];
            occupancy[x, y] <= 1;
            occupancy[x, y] <= sum{ c in COLORS: colorsOverlayGrid[x, y, c] };
    }

    # Constraints row size limit (approximates sprites / scanline limit)
    { y in YRANGE:
        rowLimit_$y$:
            sum{ x in XRANGE: occupancy[x, y] } + sum{ x in XRANGE, c in COLORS: colorsOverlayFree[x, y, c] }  <= OVERLAY_ROW_SIZE_LIMIT;
    }

    # Boolean constraint to get all free colors into a global
    { c in COLORS:
        { x in XRANGE, y in YRANGE:
            colorsOverlayTotal_$x$_$y$_$c$:
                colorsOverlayTotal[c] >= colorsOverlayFree[x, y, c];
        }
        colorsOverlayTotalClampToFree_$c$:
            colorsOverlayTotal[c] <= sum{ x in XRANGE, y in YRANGE: colorsOverlayFree[x, y, c] };
        colorsOverlayTotalClampToOne_$c$:
            colorsOverlayTotal[c] <= 1;
    }

    # Ensure each individual free overlay color is present in at least one optimised overlay palette
    { c in COLORS:
        freeColorInAnyPalette_$c$_$2$:
            sum{ p in 0..MAX_SPR_PALETTES-1: palettesOverlay[p, c] } >= colorsOverlayTotal[c];
    }

    # Constraint for palette color limit
    { p in 0..MAX_SPR_PALETTES-1:
        paletteColorLimit_$p$:
            sum{ c in COLORS: palettesOverlay[p, c] } <= CELL_COLOR_LIMIT;
    }

    # Constraint to ensure total overlay colors don't exceed overlay palette
    overlayColorLimit:
        sum{ c in COLORS: colorsOverlayTotal[c] } <= MAX_COLORS_OVERLAY;

    # Constraint for colors-subset-of-palette
    { x in XRANGE, y in YRANGE, p in 0..MAX_SPR_PALETTES-1:
        colorsOverlaymustBeSubsetOfPalette_$x$_$y$_$p$_$2$:
            usesPaletteOverlay[x, y, p] * colorsOverlayGrid[x, y, COLORS] <= palettesOverlay[p, COLORS];
    }

    # Constraint to ensure that every BG cell's set of colors are a subset of some palette's colors
    { x in XRANGE, y in YRANGE:
        cellColorsInPalette_$x$_$y$:
            sum{ p in 0..MAX_SPR_PALETTES-1: usesPaletteOverlay[x, y, p] } = 1;
    }

